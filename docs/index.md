---
page_title: "vpsAdmin Provider for vpsFree.cz"
subcategory: ""
description: |-
  Provider for vpsFree.cz using the vpsAdmin API
---

# vpsAdmin Provider
vpsAdmin provider can be used to manage resources on
[vpsFree.cz](https://vpsfree.org) using vpsAdmin API. The provider supports
basic management of virtual servers (VPS) and public keys for access over SSH.

## Obtaining API authentication token
The provider needs an authentication token to the vpsAdmin API. The token can
be obtained using any of [HaveAPI clients](https://github.com/vpsfreecz/haveapi),
but the provider also comes with a simple CLI utility
[get-token](https://github.com/vpsfreecz/terraform-provider-vpsadmin/tree/master/get-token).

The authentication token should be put in an arbitrary tfvars file, e.g.
`token.auto.tfvars`:

```
vpsadmin_token = "your token"
```

## Example usage
```terraform
provider "vpsadmin" {
  # vpsAdmin API URL (default)
  # Can be also changed using environment variable VPSADMIN_API_URL
  # api_url = "https://api.vpsfree.cz"

  # Authentication token
  # Can be also set using environment variable VPSADMIN_API_TOKEN
  auth_token = var.vpsadmin_token
}

resource "vpsadmin_ssh_key" "my-key" {
  label = "My public key"

  # Set your public key here. The file has to contain exactly one public key.
  key = file("~/.ssh/my_key.pub")
}

resource "vpsadmin_vps" "my-vps" {
  # Location label
  # Possible values
  #   - using vpsfree-client: vpsfreectl location list -o label
  #   - using curl: curl https://api.vpsfree.cz/locations
  location = "Praha"

  # OS template name
  # Possible values
  #   - using vpsfree-client: vpsfreectl os_template list -o name
  #   - using curl: curl https://api.vpsfree.cz/os_templates
  os_template = "ubuntu-20.04-x86_64-vpsadminos-minimal"

  # vpsAdmin-managed hostname
  hostname = "my-vps"

  # Number of CPU cores
  cpu = 8

  # Available memory in MB
  memory = 4096

  # Root dataset size in MB
  diskspace = 122880

  # Public keys deployed to /root/.ssh/authorized_keys
  ssh_keys = [
    vpsadmin_ssh_key.my-key.id,
  ]

  # Install nginx once created
  provisioner "remote-exec" {
    inline = [
      "export PATH=$PATH:/usr/bin",
      "apt-get update",
      "apt-get -y install nginx",
    ]

    connection {
      type = "ssh"
      host = vpsadmin_vps.my-vps.public_ipv4_address

      # Set your private key here
      private_key = file("~/.ssh/my_key")
      user        = "root"
      timeout     = "2m"
    }
  }
}
```

See more at https://github.com/vpsfreecz/terraform-provider-vpsadmin/tree/master/examples.

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- **auth_token** (String) The authentication token for API operations.

### Optional

- **api_url** (String) The URL to use for the vpsAdmin API.
